%--------------------------------------------------------
% Rules for graphs created with UDPipe (Default Italian)
% AS May 25
%
% --merge: Codings are added to the table in the row of 'node'
% ==> run dql.py WITHOUT flag --code_head
%         (--code_head would add attributes to the row of 'addlemma')
%--------------------------------------------------------

%--------------------------------------------------------
% 1. OBJECT CLITICS
%--------------------------------------------------------

% ======= direct object person 3
% coding attribute=clitic_acc value=obj3 node=V addlemma=PRO
pattern {
    V [upos=/AUX|VERB/];
    PRO [form=/^[lL].*$/, PronType="Prs"];
    V -[obj]-> PRO;
}

% ======= direct object person 1-2
% coding attribute=clitic_acc value=obj12 node=V addlemma=PRO
pattern {
    V [upos=/AUX|VERB/];
    PRO [form=/^[mtvnMTVN].*$/, PronType="Prs"];
    V -[obj]-> PRO;
}

% ======= indirect object person 3
% coding attribute=clitic_dat value=iobj3 node=V addlemma=PRO
pattern {
    V [upos=/AUX|VERB/];
    PRO [form=/^[lgLG].*$/, PronType="Prs"];
    V -[iobj]-> PRO;
}

% ======= indirect object person 1-2
% coding attribute=clitic_dat value=iobj12 node=V addlemma=PRO
pattern {
    V [upos=/AUX|VERB/];
    PRO [form=/^[mtvcMTVC].*$/, PronType="Prs"];
    V -[iobj]-> PRO;
}

% ======= ci
% coding attribute=clitic_adv value=loc node=V addlemma=PRO
pattern {
    V [upos=/AUX|VERB/];
    PRO [form=re"^[cC][i']", PronType="Prs"];
    V -[expl|expl:comp|obl:mod]-> PRO;
}

%--------------------------------------------------------
% 2. REFLEXIVE CLITICS
%    usually parses reflexives as  expl:pv  or  obj
%--------------------------------------------------------

% ======= reflexive person 3
% coding attribute=refl value=3 node=V addlemma=PRO
pattern {
    V [upos=/AUX|VERB/];
    PRO [Clitic="Yes", Person="3", PronType="Prs"];
    V -[re".*"]-> PRO;
}

% ======= reflexive person 1-2
% coding attribute=refl value=12 node=V addlemma=PRO
pattern {
    V [upos=/AUX|VERB/];
    PRO [Clitic="Yes", Person=/[12]/, PronType="Prs"];
    V -[re".*"]-> PRO;
}

%--------------------------------------------------------
% 3. POTENTIAL DATIVE ARGUMENTS (a-PPs)
%    a-PPs are parsed as arguments (obl:arg) or modifiers (obl:mod)
%--------------------------------------------------------

% ======= a PP
% coding attribute=a_pp value=mod node=V addlemma=P
pattern {
    V [upos=/AUX|VERB/];
%    P [form=/^(a|alla|alle|agli|al|all')/];   % apostrophe casts error in slashed regex
    P [form=re"^(a|alla|alle|agli|al|all')"];
    N [upos=/NOUN|PROPN/];
    V -[obl|obl:mod]-> N;
    N -[case]-> P;
}

%--------------------------------------------------------
% 4. VERB PARTICLES
%--------------------------------------------------------

% ======= giÃ¹ etc.
% coding attribute=part value=adv node=V addlemma=P
pattern {
    V [upos=/AUX|VERB/];
    P [ ];
    V -[advmod]-> P;
    V << P;
}

%